##  JAGS model, to analyze the pre-post-burn data from Ichauway, collected Spring 2016.
##
##
##  24 Jul 2017
#####################################

data {  
  p.vals.dim <- dim(p.vals)
  y.dim <- dim(y)
}
  
model {

sigma ~ dunif(0, 100)

##  Data augmentation:
psi ~ dunif(0,1)


for (i in 1:y.dim[1]) {             ##  y.dim[1] = number of observed + augmented burrows
                  
  w[i] ~ dbern(psi)

  dist[i] ~ dunif(0, Bx[y.dim[2]])   ##  take Bx, the outer limit of the surveyed area (strip-width), from 1) the pre-burn survey whn analyzing only the pre-burn data; 2) the post-burn survey, in the joint analysis (the value is very nearly equal for both, in fact)

  dL[i] <- 5 * diam.bin[i]    ##  lower limit of possible diameter of burrow i
  
  ##  Choose diameter of burrow i (only used for augmented burrows, since `diam` has values coming in as data for observed burrows (NA for augmented burrows).
  diam[i] ~ dunif(dL[i], dL[i] + 5)

for (j in 1:y.dim[2]) {

    p[i,j] <- exp(-((dist[i]^2)/(2*sigma^2)))    ##  the half-normal detection function, with no adjustment.

    mu[i,j] <- w[i] * p[i,j]
    y[i,j] ~ dbern(mu[i,j])
  }

}

###  Derived:
N <- sum(w)
D <- N*(10000/(2*Bx[y.dim[2]]*surv.len))  # burrows per hectare

###  Predicted values, for plotting out the detection curves:
for (i in 1:p.vals.dim[1]) {
  for (j in 1:y.dim[2]) {
    p.pred[i,j] <- exp(-((p.vals[i,2]^2)/(2*sigma^2)))   ##  p.vals[,2] is the distance column
  }
}

}
